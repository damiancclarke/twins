/* Regressions 3.01              damiancclarke             yyyy-mm-dd:2013-04-15
*---|----1----|----2----|----3----|----4----|----5----|----6----|----7----|----8
*/

/* This file contains all regression results that are being used in the Twin 
Instrument paper.  These include
	- Summary.xls (sum stats)
   - Summary_Birthorder.xls
   - Twin_Predict.xls

This do file uses as input the data file DHS_Twins, which is generated by the
file Twin_Setup.do.  Currently it includes all DHS files available up until 
January 2013.  Files are downloaded and imported to Stata using the files 
DHS_Import.py and DHS_Multicountry.do.

*/


clear all
version 11.2
cap log close
set more off
set mem 2000m

*******************************************************************************
*** (0) Globals and Local
*******************************************************************************
global Base ~/investigacion/Activa/Twins
global Data $Base/Data
global Results ~/investigacion/Activa/Twins/Results
global Tables $Results/Outreg
log using "$Base/Log/Twin_Regressions.log", text replace

*Regression controls
global sumstats bord fert idealnumkids agemay educf educ highschool height bmi/*
*/ infantmortality childmortality

global twinpredict agemay magesq educf_1_4 educf_5_6 educf_7_10 educf_11plus /*
*/ height bmi agefirstbirth i.child_yob i._cou 
global twinout agemay magesq educf_1_4 educf_5_6 educf_7_10 educf_11plus /*
*/ height bmi agefirstbirth 

global S educf_1_4 educf_5_6 educf_7_10 educf_11pl
global SH height bmi educf_1_4 educf_5_6 educf_7_10 educf_11pl

global basecont malec agemay magesq agefirstbirth i._cou i.year_birth i.age
global basecont2 malec agemay magesq agefirstbirth i.year_birth i.age /*i.bord*/
global baseout malec agemay magesq agefirstbirth
global morecont height bmi educf_1_4 educf_5_6 educf_7_10 educf_11pl
global moreout height bmi educf_1_4 educf_5_6 educf_7_10 educf_11pl

*SWITCHES
local sumstats no
local twin no
local ols no
local bord no
	local bordfinal no
	local bordafter no
	local bordall no
local gend no
local income no
local bordpretwin no
	local bordafter_pre no
	local bordall_pre no
local plusgroups no
	local notwins no
	local twinsplus no
local genderIV yes


* FOLDERS
cap mkdir "$Results/Outreg"

* FILES (these are the names with which to append outreg files)
local Sum Summary
local SumBord Summary_Birthorder
local twins Twin_Predict
local twinbord TwinBord_Final
local twinafter TwinAfter_bord
local twinbordafter TwinBord_After
local twinbordall TwinBord_All
local twinbordafter_pre TwinBord_After_pre
local twinbordall_pre TwinBord_All_pre
local gend_twinbordall TwinBordAll_gender_zscoreonly
local gend_twinbordafter TwinBordAfter_gender_zscoreonly
local inc_twinbordall TwinBordAll_income_zscoreonly
local inc_twinbordafter TwinBordAfter_income_zscoreonly
local IVplusgroups IVplusgroups
local IVplusgroups_first IVplusgroups_first
local IVplusgroups_after IVplusgroups_after
local IVplusgroups_after_first IVplusgroups_after_first
local IVplusgroups_twins IVplusgroups_twins
local IVplusgroups_first_twins IVplusgroups_first_twins
local IVplusgroups_after_twins IVplusgroups_after_twins
local IVplusgroups_after_first_twins IVplusgroups_after_first_twins



*******************************************************************************
*** (0) Setup (+ discretionary choices)
*******************************************************************************
use "$Data/DHS_twins_new"

replace bmi=. if bmi>42
replace height=. if height>240
replace height=. if height<80
replace educ=. if age<6
replace educ=. if educ>25
replace educf=. if educf>25

keep if _merge==3	

*******************************************************************************
*** (1) Summary Stats
*******************************************************************************
if `"`sumstats'"'=="yes" {
	***************************************************************************
	*** (1a) Graphical
	***************************************************************************
	cap mkdir "$Results/Graphs"

	*(1a) Total births by Family Type
	twoway kdensity fert if twinfamily==1, bw(2) || kdensity fert if ///
	twinfamily==0, bw(2) bgcolor(white) legend(label(1 "Twin Family") ///
	label(2 "Singleton Family")) ytitle("Density") graphregion(color(white)) ///
	title("Total births by Family Type") xtitle("total children ever born") 
	graph save "$Results/Graphs/famsize", replace
	graph export "$Results/Graphs/famsize.eps", as(eps) replace

	*(1a) Total births by Family Type (for families who exceed desired fertility)
	twoway kdensity fert if twinfamily==1&fert>desiredfert_region, bw(2) || ///
	kdensity fert if twinfamily==0&fert>desiredfert_r&twin_bord>=desiredfert_r, ///
	bw(2) bgcolor(white) legend(label(1 "Twin Family") ///
	label(2 "Singleton Family")) ytitle("Density") graphregion(color(white)) ///
	title("Total births by Family Type") xtitle("total children ever born") ///
	subtitle("For families who exceed regional desired family size")
	graph save "$Results/Graphs/famsize_exceed", replace
	graph export "$Results/Graphs/famsize_exceed.eps", as(eps) replace
	
	*(2) Twins by bord
	sum twind	
	local meantwin %6.4f r(mean)
	preserve
	collapse twind, by(bord)
	line twind bord if bord<11, title("Twinning by birth order") ///
	ytitle("Fraction twins") xtitle("Birth Order") yline(0.0189) ///
	note("Single births are 1-frac(twins). Total fraction of twins is 0.0189") ///
	graphregion(color(white)) bgcolor(white)
	graph save "$Results/Graphs/twinbybord", replace
	graph export "$Results/Graphs/twinbybord.eps", as(eps) replace
	restore

	*(3) Desired family size
	local note1 "Total sample of families is 1,416,924."
	local note2 " Ideal size 10 refers to >=10.  2.61% of families report 'up"
	local note3 " to god', and 4.39% provide a non-numeric response"
	
	preserve
	collapse idealnumkids v613, by(id)
	gen idealnum=v613 if v613<=10
	replace idealnum=10 if v613>10&v613<98
	hist idealnum, note("`note1'" "`note2'" "`note3'") bcolor(navy) frac ///
	bgcolor(white) graphregion(color(white)) xtitle("Ideal Family Size") ///
	title("Self Reported Ideal Family Size")
	graph save "$Results/Graphs/idealfamsize", replace
	graph export "$Results/Graphs/idealfamsize.eps", as(eps) replace	
	restore

	***************************************************************************
	*** (1b) Numerical
	***************************************************************************
	*(A) characteristics of families who want different ideal sizes (P.Dev idea)
	areg idealnumkids educf_* agefirstbirth poor1 height bmi i.year_birth/*
	*/ i.child_yob i.agemay [pw=sweight], a(_cou) cluster(_cou)
	outreg2 educf_0 educf_1_4 educf_5_6 educf_7_10 agefirstbirth poor1 /*
	*/educ height bmi using $Results/Outreg/fampref.xls, excel replace	

	*(B) 

	***************************************************************************
	*** (1c) Tables
	***************************************************************************
	* (A) By twins
	local opts cells("count mean sd min max")

	estpost sum $sumstats 
	estout using "$Tables/`Sum'.xls", replace `opts' title("all")
	estpost sum $sumstats if twind==0 
	estout using "$Tables/`Sum'.xls", append `opts' title("All Singleton")
	estpost sum $sumstats if twind==1 
	estout using "$Tables/`Sum'.xls", append `opts' title("All Twins")

	estpost sum $sumstats if twind==0 & income=="low" 
	estout using "$Tables/`Sum'.xls", append `opts' title("Single, Low Inc")
	estpost sum $sumstats if twind==1 & income=="low"
	estout using "$Tables/`Sum'.xls", append `opts' title("Twin, Low Inc")
	estpost sum $sumstats if twind==0 & income=="mid" 
	estout using "$Tables/`Sum'.xls", append `opts' title("Single, Middle Inc")
	estpost sum $sumstats if twind==1 & income=="mid" 
	estout using "$Tables/`Sum'.xls", append `opts' title("Twin, Middle Inc")

	*(B) By birthorder
	foreach num of numlist 1(1)10 {	
	estpost sum $sumstats if bord==`num' 
	estout using "$Tables/`SumBord'.xls", append `opts' title("Birth order=`num'")
	}
}

********************************************************************************
**** (2) Twin predict regressions
**** Note to table:  All specifications include a full set of year of birth and 
**** country dummies and are estimated as linear probability models. Results are 
**** robust to the inclusion of birth order dummies (rather than a single 
**** discrete variable).  Twin is multiplied by 100 for presentation. Height is 
**** measured in cm and BMI in points. Maternal education is included as dummies
**** by level, with no education the omitted base.
********************************************************************************
if `"`twin'"'=="yes" {
	reg twind100 bord $twinpredict [pw=sweight], cluster(_cou)
	outreg2 $twinout using "$Results/Outreg/`twins'.xls", excel replace

	* test by country income level
	foreach inc in low mid {
		reg twind100 bord $twinpredict [pw=sweigh] if income=="`inc'", cluster(_cou)
		outreg2 $twinout using "$Results/Outreg/`twins'.xls", excel append
	}

	local cond1 child_yob>1989
	local cond2 child_yob<=1989

	* test for pre- and post-IVF
	foreach cond in `cond1' `cond2' {
		reg twind100 bord $twinpredict [pw=sweigh] if `cond', cluster(_cou)
		outreg2 $twinout using "$Results/Outreg/`twins'.xls", excel append
	}

}

********************************************************************************
**** (3) Simple OLS of Q-Q (can then apply Altonji)
********************************************************************************
if `"`ols'"'=="yes" {
	cap rm "$Results/Outreg/QQ_ols.xls"
	cap rm "$Results/Outreg/QQ_ols.txt"
	local out "$Results/Outreg/QQ_ols.xls"

	foreach y of varlist school_zscore highschool childmortality infantmortality {
		reg `y' fert $basecont [pw=sweight], cluster(_cou)
		outreg2 fert $baseout using `out', excel append
		reg `y' fert $basecont $S [pw=sweight], cluster(_cou)
		outreg2 fert $baseout $S using `out', excel append
		reg `y' fert $basecont $SH [pw=sweight], cluster(_cou)	
		outreg2 fert $baseout $SH using `out', excel append
	}

	foreach inc in low mid {
		foreach y of varlist school_zscore educ attendance highschool {
			reg `y' fert $basecont [pw=sweight] if income==`"`inc'"', cluster(_cou)
			outreg2 fert using `out', excel append
			reg `y' fert $basecont $S [pw=sweight] if income==`"`inc'"', cluster(_cou)
			outreg2 fert $baseout $S using `out', excel append
			reg `y' fert $basecont $SH [pw=sweigh] if income==`"`inc'"', cluster(_cou)	
			outreg2 fert $baseout $SH using `out', excel append
		}
	}
}


********************************************************************************
**** (4) Reduced Form Regressions on twin by birth order (this is by birth order
**** so that we are comparing families of size q with families of size q+1 due
**** to twins).
********************************************************************************
if `"`bord'"'=="yes" {
	if `"`bordfinal'"' == "yes" {
		local cond agefirstbirth>=14&age<22
		cap rm "$Results/Outreg/`twinbord'.xls"
		cap rm "$Results/Outreg/`twinbord'.txt"

		local out "$Results/Outreg/`twinbord'.xls"
		foreach birth of numlist 2(1)7 {
			dis in yellow "We are on birth number `birth' of 7"	
			local cond2 ((fert==idealnumkids&FAMtwinbindsfinal==0&fert==`birth')|/*
			*/(fert==idealnumkids+1&fert==`birth'+1&FAMtwinbindsfinal==1))

			foreach outcome of varlist school_zscore highschool childmort infantmort {
				reg `outcome' T_Final T_FinalXtwin $basecont $morecont [pw=sweight] /*
				*/ if `cond'&`cond2'
				outreg2 T_Final T_FinalXtwin $baseout $moreout using `out', excel append

				reg `outcome' T_Bind T_BindXtwin $basecont $morecont [pw=sweight] /*
				*/ if `cond'&`cond2'&e(sample)
				outreg2 T_Bind T_BindXtwin $baseout $moreout using `out', excel append
			}
		}
	}

	if `"`bordafter'"' == "yes" {
		local cond agefirstbirth>=14&age<22
		cap rm "$Results/Outreg/`twinbordafter'.xls"
		cap rm "$Results/Outreg/`twinbordafter'.txt"

		local out "$Results/Outreg/`twinbordafter'.xls"
		foreach birth of numlist 2(1)7 {
			dis in yellow "We are on birth number `birth' of 7"	
			local cond2 ((T_After_region==1&fert==`birth'+1)|(T_After_region==0&fert==`birth'))

			foreach outcome of varlist school_zscore highschool childmort infantmort {
				reg `outcome' T_After_region $basecont $morecont [pw=sweight] if `cond'&`cond2'
				outreg2 T_After_region $baseout $moreout using `out', excel append

				reg `outcome' T_After_region $basecont [pw=sweight] if `cond'&`cond2'&e(sample)
				outreg2 T_After_region $baseout using `out', excel append
			}
		}
   }
  
	if `"`bordall'"' == "yes" {
		local cond agefirstbirth>=14&age<22
		cap rm "$Results/Outreg/`twinbordall'.xls"
		cap rm "$Results/Outreg/`twinbordall'.txt"

		local out "$Results/Outreg/`twinbordall'.xls"
		foreach birth of numlist 2(1)7 {
			dis in yellow "We are on birth number `birth' of 7"	
			local cond2 ((T_Twin==1&fert==`birth'+1)|(T_Twin==0&fert==`birth'))

			foreach outcome of varlist school_zscore highschool childmort infantmort {
				reg `outcome' T_Twin $basecont $morecont [pw=sweight] if `cond'&`cond2'
				outreg2 T_Twin $baseout $moreout using `out', excel append

				reg `outcome' T_Twin $basecont [pw=sweight] if `cond'&`cond2'&e(sample)
				outreg2 T_Twin $baseout using `out', excel append
			}
		}
	}	
}

********************************************************************************
**** (5a) Reduced Form Regressions on twin by birth order (by gender)
********************************************************************************
if `"`gend'"' == "yes" {	
	** remove outreg files
	foreach x in M F {
		preserve
		keep if gender=="`x'"
		local cond agefirstbirth>=14&age<22

		cap rm "$Results/Outreg/`gend_twinbordafter'_`x'.xls"
		cap rm "$Results/Outreg/`gend_twinbordafter'_`x'.txt"
		cap rm "$Results/Outreg/`gend_twinbordall'_`x'.xls"
		cap rm "$Results/Outreg/`gend_twinbordall'_`x'.txt"

		local loop 1
		foreach cond2 in cond_aft /*cond_all*/ {
			if `loop'==1 local out "$Results/Outreg/`gend_twinbordafter'_`x'.xls"
			else if `loop'==2 local out "$Results/Outreg/`gend_twinbordall'_`x'.xls"		

			foreach y of varlist school_zscore /*attendance highschool*/ {
				foreach birth of numlist 2(1)7 {

					local cond_aft ((T_After==1&fert==`birth'+1)|(T_After==0&fert==`birth'))
					local cond_all ((T_Twin==1&fert==`birth'+1)|(T_Twin==0&fert==`birth'))

					reg `y' T_After $basecont $morecont [pw=sweight] if `cond'&``cond2''
					outreg2 T_After using `out', excel append

					reg `y' T_After $basecont [pw=sweight] if `cond'&``cond2''&e(sample)
					outreg2 T_After using `out', excel append
				}
			}
			local ++loop
		}
		restore
	}
}

********************************************************************************
**** (5b) Reduced Form Regressions on twin by birth order (by income)
********************************************************************************
if `"`income'"' == "yes" {
	** remove outreg files
	foreach x in low mid {
		preserve
		keep if income=="`x'"
		local cond agefirstbirth>=14&age<22

		cap rm "$Results/Outreg/`inc_twinbordafter'_`x'.xls"
		cap rm "$Results/Outreg/`inc_twinbordafter'_`x'.txt"
		cap rm "$Results/Outreg/`inc_twinbordall'_`x'.xls"
		cap rm "$Results/Outreg/`inc_twinbordall'_`x'.txt"

		local loop 1
		foreach cond2 in cond_aft /*cond_all*/ {

			if `loop'==1 local out "$Results/Outreg/`inc_twinbordafter'_`x'.xls"
			else if `loop'==2 local out "$Results/Outreg/`inc_twinbordall'_`x'.xls"		

			foreach y of varlist school_zscore /*attendance highschool*/ {
				foreach birth of numlist 2(1)7 {

					local cond_aft ((T_After==1&fert==`birth'+1)|(T_After==0&fert==`birth'))
					local cond_all ((T_Twin==1&fert==`birth'+1)|(T_Twin==0&fert==`birth'))

					reg `y' T_After $basecont $morecont [pw=sweight] if `cond'&``cond2''
					outreg2 T_After using `out', excel append

					reg `y' T_After $basecont [pw=sweight] if `cond'&``cond2''&e(sample)
					outreg2 T_After using `out', excel append

				}
			}
			local ++loop
		}
		restore
	}
}


********************************************************************************
**** (6) Reduced Form Regressions on pre-twins by birth order
********************************************************************************
if `"`bordpretwin'"'=="yes" {
	*keep if income=="low"
	if `"`bordafter_pre'"' == "yes" {
		local cond agefirstbirth>=14&age<22
		cap rm "$Results/Outreg/`twinbordafter_pre'.xls"
		cap rm "$Results/Outreg/`twinbordafter_pre'.txt"

		local out "$Results/Outreg/`twinbordafter_pre'.xls"
		foreach birth of numlist 2(1)8 {
			dis in yellow "We are on birth number `birth' of 8"	
			local cond2 ((T_After==1&fert==`birth'+1&pretwinafter==1)|/*
			*/(T_After==0&fert==`birth'))

			foreach outcome of varlist school_zscore highschool childmort infantmort {
				reg `outcome' T_After $basecont $morecont [pw=sweight] if `cond'&`cond2'
				outreg2 T_After $baseout $moreout using `out', excel append

				reg `outcome' T_After $basecont [pw=sweight] if `cond'&`cond2'&e(sample)
				outreg2 T_After $baseout using `out', excel append

			}
		}
	}

	if `"`bordall_pre'"' == "yes" {
		local cond agefirstbirth>=14&age<22
		cap rm "$Results/Outreg/`twinbordall_pre'.xls"
		cap rm "$Results/Outreg/`twinbordall_pre'.txt"

		local out "$Results/Outreg/`twinbordall_pre'.xls"
		foreach birth of numlist 2(1)8 {
			dis in yellow "We are on birth number `birth' of 8"	
			local cond2 ((T_Twin==1&fert==`birth'+1&pretwin==1)|/*
			*/(T_Twin==0&fert==`birth'))

			foreach outcome of varlist school_zscore highschool childmort infantmort {
				reg `outcome' T_Twin $basecont $morecont [pw=sweight] if `cond'&`cond2'
				outreg2 T_Twin $baseout $moreout using `out', excel append

				reg `outcome' T_Twin $basecont [pw=sweight] if `cond'&`cond2'&e(sample)
				outreg2 T_Twin $baseout using `out', excel append
			}
		}
	}
}

********************************************************************************
**** (7) Instrument as per Angrist et al using "+ groups"
********************************************************************************
**** Here the idea is to use groups 2+, 3+ etc., where the 2+ groups refers to 
**** all first born children in families of size two or more, the 3+ group ref-
**** ers to all first and second born 
********************************************************************************
if `"`plusgroups'"'=="yes" {

   global basecontIV malec agemay magesq agefirstbirth

   tab _cou, gen(_country)
   tab year_birth, gen(_yb)
   tab age, gen(_age)
   
   if `"`notwins'"'=="yes" {
      local cond agefirstbirth>=14&age<22
      local cond2 agefirstbirth>=14&age<22&fert>desiredfert_region

      foreach file in IVplusgroups IVplusgroups_first IVplusgroups_after /*
      */ IVplusgroups_after_first {
      cap rm "$Results/Outreg/``file''.xls"
      cap rm "$Results/Outreg/``file''.txt"
      }

      local out "$Results/Outreg/`IVplusgroups'.xls"
      local outfirst "$Results/Outreg/`IVplusgroups_first'.xls"	
      local out2 "$Results/Outreg/`IVplusgroups_after'.xls"
      local out2first "$Results/Outreg/`IVplusgroups_after_first'.xls"

      foreach num in two three four five /*six seven*/ {
         foreach y of varlist school_zscore highschool /*childmort*/ infantmort {
            *ALL TWINS
            ivreg2 `y' (fert=twin_`num'_fam) $basecontIV $morecont _country* /*
            */ _yb* _age* if `cond'&`num'_plus==1, savefirst
            outreg2 fert $baseout $moreout using `out', excel append
            est restore _ivreg2_fert
            outreg2 twin_`num'_fam $baseout $moreout using `outfirst', excel append

            ivreg2 `y' (fert=twin_`num'_fam) $basecontIV _country* _yb* _age* /*
            */ if `cond'&`num'_plus==1& e(sample), savefirst
            outreg2 fert $baseout using `out', excel append
            est restore _ivreg2_fert
            outreg2 twin_`num'_fam $baseout using `outfirst', excel append


            *TWINS AFTER DESIRED ONLY
            ivreg2 `y' (fert=twin_`num'_fam) $basecontIV $morecont _country* _yb* /*
            */ _age* if `cond2'&`num'_plus==1, savefirst
            outreg2 fert $baseout $moreout using `out2', excel append
            est restore _ivreg2_fert
            outreg2 twin_`num'_fam $baseout $moreout using `out2first', excel append

            ivreg2 `y' (fert=twin_`num'_fam) $basecontIV _country* _yb* _age* if /*
            */ `cond2'&`num'_plus==1& e(sample), savefirst
            outreg2 fert $baseout using `out2', excel append
            est restore _ivreg2_fert
            outreg2 twin_`num'_fam $baseout using `out2first', excel append
         }
      }
   }
   if `"`twinsplus'"'=="yes" {
      local cond agefirstbirth>=14&age<22
      local cond2 agefirstbirth>=14&age<22&fert>desiredfert_region

      foreach file in IVplusgroups_twins IVplusgroups_first_twins /*
      */ IVplusgroups_after_twin IVplusgroups_after_first {
      cap rm "$Results/Outreg/``file''.xls"
      cap rm "$Results/Outreg/``file''.txt"
      }

      local out "$Results/Outreg/`IVplusgroups_twins'.xls"
      local outfirst "$Results/Outreg/`IVplusgroups_first_twins'.xls"	
      local out2 "$Results/Outreg/`IVplusgroups_after_twins'.xls"
      local out2first "$Results/Outreg/`IVplusgroups_after_first_twins'.xls"

      foreach num in two three four five /*six seven*/ {
         foreach y of varlist school_zscore highschool /*childmort*/ infantmort {
            *ALL TWINS
            ivreg2 `y' (fert=twin_`num'_fam) $basecontIV $morecont _country* /*
            */ _yb* _age* if `cond'&`num'_plus_withtwin==1, savefirst
            outreg2 fert $baseout $moreout using `out', excel append
            est restore _ivreg2_fert
            outreg2 twin_`num'_fam $baseout $moreout using `outfirst', excel append

            ivreg2 `y' (fert=twin_`num'_fam) $basecontIV _country* _yb* _age* /*
            */ if `cond'& `num'_plus_withtwin==1& e(sample), savefirst
            outreg2 fert $baseout using `out', excel append
            est restore _ivreg2_fert
            outreg2 twin_`num'_fam $baseout using `outfirst', excel append

            
            *TWINS AFTER DESIRED ONLY
            ivreg2 `y' (fert=twin_`num'_fam) $basecontIV $morecont _country* /*
            */ _yb* _age* if `cond2'&`num'_plus_withtwin==1, savefirst
            outreg2 fert $baseout $moreout using `out2', excel append
            est restore _ivreg2_fert
            outreg2 twin_`num'_fam $baseout $moreout using `out2first', excel append

            ivreg2 `y' (fert=twin_`num'_fam) $basecontIV _country* _yb* _age* /*
            */ if `cond2'& `num'_plus_withtwin==1& e(sample), savefirst
            outreg2 fert $baseout using `out2', excel append
            est restore _ivreg2_fert
            outreg2 twin_`num'_fam $baseout using `out2first', excel append
         }
      }
   }
}

********************************************************************************
**** (8) Instrument as per Angrist et al using gender groups
********************************************************************************
if `"`genderIV'"'=="yes" {



}
