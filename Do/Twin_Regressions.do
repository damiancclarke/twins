/* Regressions 3.00              damiancclarke             yyyy-mm-dd:2013-04-15
*---|----1----|----2----|----3----|----4----|----5----|----6----|----7----|----8
*/

/* This file contains all regression results that are being used in the Twin 
Instrument paper.  These include
	-
	-

This do file uses as input the data file DHS_Twins, which is generated by the
file Twin_Setup.do.  Currently it includes all DHS files available up until 
January 2013.  Files are downloaded and imported to Stata using the files 
DHS_Import.py and DHS_Multicountry.do.

*/


clear all
version 11.2
cap log close
set more off
set mem 2000m

*******************************************************************************
*** (0) Globals and Local
*******************************************************************************
global Base ~/investigacion/Activa/Twins
global Data $Base/Data
global Results ~/investigacion/Activa/Twins/ResultsNEW
log using "$Base/Log/Twin_Regressions.log", text replace

*Regression controls
global sumstats bord fert idealnumkids agemay educf educ attendance height bmi

global twinpredict agemay magesq educf_1_4 educf_5_6 educf_7_10 educf_11plus /*
*/ height bmi agefirstbirth i.child_yob i._cou 
global twinout agemay magesq educf_1_4 educf_5_6 educf_7_10 educf_11plus /*
*/ height bmi agefirstbirth 

global S educf_1_4 educf_5_6 educf_7_10 educf_11pl
global SH height bmi educf_1_4 educf_5_6 educf_7_10 educf_11pl

global basecont malec agemay magesq agefirstbirth i._cou i.year_birth i.age /*i.bord*/
global basecont2 malec agemay magesq agefirstbirth i.year_birth i.age /*i.bord*/
global baseout malec agemay magesq agefirstbirth
global morecont height bmi educf_1_4 educf_5_6 educf_7_10 educf_11pl
global moreout height bmi educf_1_4 educf_5_6 educf_7_10 educf_11pl

*SWITCHES
local ESPE no
local sumstats no
local twin no
local ols no
local pooled no
	local bind no
	local final no
	local after no
local bord yes
	local bordfinal no
	local bordafter yes
	local bordall yes
local gend no
local income no
local bordpretwin yes
	local bordafter_pre yes
	local bordall_pre yes
local IV no
	local IVbordafter no
	local IVbordall no
local plusgroups yes


* FOLDERS
cap mkdir "$Results/Outreg"

* FILES (these are the names with which to append outreg files)
local twinbind BindTwins
local twinfinal FinalTwins
local twinbord TwinBord_Final
local twinafter TwinAfter_bord
local twinbordafter TwinBord_After_zscoreonly
local twinbordall TwinBord_All_zscoreonly
local twinbordafter_pre TwinBord_After_pre_only_lowinc
local twinbordall_pre TwinBord_All_pre_only_lowinc
local gend_twinbordall TwinBordAll_gender_zscoreonly
local gend_twinbordafter TwinBordAfter_gender_zscoreonly
local inc_twinbordall TwinBordAll_income_zscoreonly
local inc_twinbordafter TwinBordAfter_income_zscoreonly
local IVtwinbordall TwinBordAll_IV
local IVtwinbordafter TwinBordAfter_IV

*******************************************************************************
*** (0) Setup (+ discretionary choices)
*******************************************************************************
use "$Data/DHS_twins"
***THIS IS NEW AND GETS RID OF ALL TWIN FAMILIES FROM CONTROL
*foreach treat in Final Bind {
*	replace T_`treat'=. if T_`treat'==0&nummultiple!=0
*	cap drop T_`treat'Xtwin
*	gen T_`treat'Xtwin=T_`treat'*twind
*}

replace bmi=. if bmi>42
replace height=. if height>240
replace height=. if height<80
replace educ=. if age<6
replace educ=. if educ>25
*drop if nummultiple>2&nummultiple<5

* Check if user written ados are installed.  If not this requires internet
cap which byhist
if _rc!=0 cap ssc install byhist

keep if _merge==3	

*******************************************************************************
*** (X) First Stage regressions ESPE
*******************************************************************************
if `"`ESPE'"'=="yes" {
	local outname Firststage

	local cond1 twin_bord<idealnumkids|twind!=1&agemay>=35
	local cond2 twin_bord>=idealnumkids|twind!=1&agemay>=35

	cap rm "$Results/Outreg/`outname'.xls"
	cap rm "$Results/Outreg/`outname'.txt"

*	reg fert twind $basecont $morecont, cluster(_cou)
*	outreg2 twind $baseout using "$Results/Outreg/`outname'.xls", excel append

	reg fert twind $basecont $morecont if twin_bord<idealnumkids-1|twind!=1&agemay>=35, cluster(_cou) 
	outreg2 twind $baseout using "$Results/Outreg/`outname'.xls", excel append

	reg fert twind $basecont $morecont if twin_bord>idealnumkids|twind!=1&agemay>=35, cluster(_cou) 
	outreg2 twind $baseout using "$Results/Outreg/`outname'.xls", excel append
}
*******************************************************************************
*** (1) Summary Stats
*******************************************************************************
if `"`sumstats'"'=="yes" {
	***************************************************************************
	*** (1a) Graphical
	***************************************************************************
	*(1) Freq distribution of twins by bord of twins
*	byhist bord  if bord<11, by(twin_birth) tw(legend(label(1 "Single Birth")/*
*	*/ label(2 "Twin Birth")) title("Birth type by birth order") ///
*	graphregion(color(white)) bgcolor(white)) frac	
*	graph export "$Results/birthtype.eps", as(eps) replace
*	graph export "$Results/birthtype.png", as(png) replace	

	twoway kdensity fert if twin_birth==1, bw(2) || kdensity fert if ///
	twin_birth==0, bw(2) legend(label(1 "Twin Family") ///
	label(2 "Singleton Family")) ytitle("Density") ///
	title("Total births by Family Type") xtitle("total children ever born") 
	graph save "$Results/famsize", replace

	*(2) % twins at last birth, % twins at last birth with family size at ideal
	*tab twintype
	*tab singletype

	*(3) Distribution of twins overlaid with distbn of family size	
	*byhist fert, by(twinfamily) tw(legend(label(1 "Singleton Family") ///
	*label(2 "Twin family")) title("Total births by family type") ///
	*graphregion(color(white)) bgcolor(white)) frac
	*graph export "$Results/familytype.eps", as(eps) replace	
	*graph export "$Results/familytype.png", as(png) replace		
	
	*(3b) Twins by bord
	sum twind	
	local meantwin %6.4f r(mean)
	preserve
	collapse twind, by(bord)
	line twind bord if bord<11, title("Twinning by birth order") ///
	ytitle("Fraction twins") xtitle("Birth Order") yline(0.0189) ///
	note("Single births are 1-frac(twins). Total fraction of twins is 0.0189") ///
	graphregion(color(white)) bgcolor(white)
	graph save "$Results/twinbybord", replace
	graph export "$Results/twinbybord.eps", as(eps) replace
	restore

	preserve
	collapse idealnumkids v613, by(id)
	gen idealnum=v613 if v613<=10
	replace idealnum=10 if v613>10&v613<98
	hist idealnum, note("Total sample of families is 1,416,924.  Ideal size 10 refers to >=10.  2.61% of families report ’up to god’ and 4.39% provide a non−numeric response") ///
	bcolor(navy) graphregion(color(white)) bgcolor(white)) ///	
	frac xtitle("Ideal Family Size")
	graph save "$Results/idealfamsize", replace
	restore

	***************************************************************************
	*** (1b) Stats
	***************************************************************************
	*(4) Do families continue with births when hitting ideal (with twins)?
	tab FAMtwinbinds FAMtwinbindsfinal

	*(5) % of unplanned births (after ideal number, also if wanted)
	tab idealfam

	*(6) characteristics of families who want different ideal sizes (P.Dev idea)
	areg idealnumkids educf_* agefirstbirth poor1 height bmi i.year_birth/*
	*/ i.child_yob i.agemay [pw=sweight], a(_cou) cluster(_cou)
	outreg2 educf_0 educf_1_4 educf_5_6 educf_7_10+ agefirstbirth poor1 /*
	*/educ height bmi using $Results/fampref.tex, tex(pr) replace
	outreg2 educf_0 educf_1_4 educf_5_6 educf_7_10 agefirstbirth poor1 /*
	*/educ height bmi using $Results/fampref.xls, excel replace	

	***************************************************************************
	*** (1c) Twin, non-twin characteristics
	***************************************************************************
	sum $sumstats
	sum twind
	sum $sumstats if twin==0
	sum $sumstats if twin>0&twin!=.
	foreach income in low mid {
	dis in yellow "`income'"
	sum twind if income=="`income'"
	sum $sumstats if twin==0 & income=="`income'", sep(15)
	sum $sumstats if twin>0&twin!=. & income=="`income'", sep(15)
	}	
}

********************************************************************************
**** (2) Twin predict regressions
**** Note to table:  All specifications include a full set of year of birth and 
**** country dummies and are estimated as linear probability models. Results are 
**** robust to the inclusion of birth order dummies (rather than a single 
**** discrete variable).  Twin is multiplied by 100 for presentation. Height is 
**** measured in cm and BMI in points. Maternal education is included as dummies
**** by level, with no education the omitted base.
********************************************************************************
if `"`twin'"'=="yes" {
	reg twind100 bord $twinpredict [pw=sweight], cluster(_cou)
	outreg2 $twinout using "$Results/Outreg/tab2_twins.xls", excel replace

	* test by country income level
	foreach inc in low mid {
		reg twind100 bord $twinpredict [pw=sweigh] if income=="`inc'", cluster(_cou)
		outreg2 $twinout using "$Results/Outreg/tab2_twins.xls", excel append
	}

	local cond1 child_yob>1989
	local cond2 child_yob<=1989

	* test for pre- and post-IVF
	foreach cond in `cond1' `cond2' {
		reg twind100 bord $twinpredict [pw=sweigh] if `cond', cluster(_cou)
		outreg2 $twinout using "$Results/Outreg/tab2_twins.xls", excel append
	}

}

********************************************************************************
**** (3) Simple OLS of Q-Q (can then apply Altonji)
********************************************************************************
if `"`ols'"'=="yes" {
	cap rm "$Results/Outreg/QQ_ols.xls"
	cap rm "$Results/Outreg/QQ_ols.txt"
	local out "$Results/Outreg/QQ_ols.xls"

	foreach y of varlist school_zscore highschool childmortality infantmortality {
		reg `y' fert $basecont [pw=sweight], cluster(_cou)
		outreg2 fert $baseout using `out', excel append
		reg `y' fert $basecont $S [pw=sweight], cluster(_cou)
		outreg2 fert $baseout $S using `out', excel append
		reg `y' fert $basecont $SH [pw=sweight], cluster(_cou)	
		outreg2 fert $baseout $SH using `out', excel append
	}

	foreach inc in low mid {
		foreach y of varlist school_zscore educ attendance highschool {
			reg `y' fert $basecont [pw=sweight] if income==`"`inc'"', cluster(_cou)
			outreg2 fert using `out', excel append
			reg `y' fert $basecont $S [pw=sweight] if income==`"`inc'"', cluster(_cou)
			outreg2 fert $baseout $S using `out', excel append
			reg `y' fert $basecont $SH [pw=sweigh] if income==`"`inc'"', cluster(_cou)	
			outreg2 fert $baseout $SH using `out', excel append
		}
	}
}


********************************************************************************
**** (4) Reduced Form Regressions on twin (this is for all children together,
**** not explicitly separating by size so that we aren't comparing families of
**** size q with families of size q+1 (due to twins)
********************************************************************************
if `"`pooled'"'=="yes" {
	local cond agefirstbirth>=14&age<22

	***************************************************************************
	*** (4a) Binding twins (no longer used)
	***************************************************************************	
	if `"`bind'"'=="yes" {
		cap rm "$Results/Outreg/`twinbind'.xls"
		cap rm "$Results/Outreg/`twinbind'.txt"	
		local out "$Results/Outreg/`twinbind'.xls"

		foreach outcome of varlist school_zscore highschool childmort infantmort {
			qui reg `outcome' T_Bind T_BindXtwin $basecont $morecont if `cond'
			reg `outcome' T_Bind $basecont [pw=sweight] if `cond'&e(sample), /*
			*/ cluster(_cou)
			outreg2 T_Bind $baseout using `out', excel append
			reg `outcome' T_Bind $basecont $morecont [pw=sweight] /*
			*/ if `cond'&e(sample), cluster(_cou)
			outreg2 T_Bind $baseout $moreout using `out', excel append
			reg `outcome' T_Bind T_BindXtwin $basecont [pw=sweight] /*
			*/ if `cond'&e(sample), cluster(_cou)
			outreg2 T_Bind T_BindXtwin $baseout using `out', excel append
			reg `outcome' T_Bind T_BindXtwin $basecont $morecont [pw=sweight] /*
			*/ if `cond', cluster(_cou)
			outreg2 T_Bind T_BindXtwin $baseout $moreout using `out', excel append
		}
	}
	***************************************************************************
	*** (4b) Final birth twins
	***************************************************************************
	if `"`final'"'=="yes" {
		cap rm "$Results/Outreg/`twinfinal'.xls"
		cap rm "$Results/Outreg/`twinfinal'.txt"
		local out2 "$Results/Outreg/`twinfinal'.xls"	

		foreach outcome of varlist school_zscore educ attendance highschool {
			qui reg `outcome' T_Final T_FinalXtwin $basecont $morecont if `cond'
			reg `outcome' T_Final $basecont [pw=sweight] if `cond'&e(sample), /*
			*/cluster(_cou)
			outreg2 T_Final $baseout using `out2', excel append
			reg `outcome' T_Final $basecont $morecont [pw=sweight] /*
			*/ if `cond'&e(sample), cluster(_cou)
			outreg2 T_Final $baseout $moreout using `out2', excel append
			reg `outcome' T_Final T_FinalXtwin $basecont [pw=sweight] /*
			*/ if `cond'&e(sample), cluster(_cou)
			outreg2 T_Final T_FinalXtwin $baseout using `out2', excel append
			reg `outcome' T_Final T_FinalXtwin $basecont $morecont [pw=sweight]/*
			*/ if `cond', cluster(_cou)
			outreg2 T_Final T_FinalXtwin $baseout $moreout using `out2', excel append
		}
	}
	***************************************************************************
	*** (4c) Twins ocurring after desired number
	***************************************************************************	
	if `"`after'"'=="yes" {
		cap rm "$Results/Outreg/`twinafter'.xls"
		cap rm "$Results/Outreg/`twinafter'.txt"	
		local out "$Results/Outreg/`twinafter'.xls"

		foreach outcome of varlist school_zscore educ attendance highschool {
			qui reg `outcome' T_After T_AfterXpretwin T_AfterXposttwin $basecont /*
			*/ $morecont if `cond'
			reg `outcome' T_After $basecont [pw=sweight] if `cond'&e(sample), /*
			*/cluster(_cou)
			outreg2 T_After $baseout using `out', excel append
			reg `outcome' T_After $basecont $morecont [pw=sweight] /*
			*/ if `cond'&e(sample), cluster(_cou)
			outreg2 T_After $baseout $moreout using `out', excel append
			reg `outcome' T_After T_AfterXpretwin T_AfterXposttwin $basecont /*
			*/ [pw=sweight] if `cond'&e(sample), cluster(_cou)
			outreg2 T_After* $baseout using `out', excel append
			reg `outcome' T_After T_AfterXpretwin T_AfterXposttwin $basecont/*
			*/ $morecont [pw=sweight] if `cond', cluster(_cou)
			outreg2 T_After* $baseout $moreout using `out', excel append
		}
	}
}

********************************************************************************
**** (5) Reduced Form Regressions on twin by birth order (this is by birth order
**** so that we are comparing families of size q with families of size q+1 due
**** to twins).
********************************************************************************
if `"`bord'"'=="yes" {
	if `"`bordfinal'"' == "yes" {
		local cond agefirstbirth>=14&age<22
		cap rm "$Results/Outreg/`twinbord'.xls"
		cap rm "$Results/Outreg/`twinbord'.txt"

		local out3 "$Results/Outreg/`twinbord'.xls"
		foreach birth of numlist 2(1)7 {
			dis in yellow "We are on birth number `birth' of 7"	
			local cond2 (fert==idealnumkids&FAMtwinbindsfinal==0&fert==`birth')|/*
			*/(fert==idealnumkids+1&fert==`birth'+1&FAMtwinbindsfinal==1)

			foreach outcome of varlist school_zscore highschool childmort infantmort {
				reg `outcome' T_Final T_FinalXtwin $basecont $morecont [pw=sweight] /*
				*/ if `cond'&`cond2'
				outreg2 T_Final T_FinalXtwin $baseout $moreout using `out3', excel append

				reg `outcome' T_Bind T_BindXtwin $basecont $morecont [pw=sweight] /*
				*/ if `cond'&`cond2'
				outreg2 T_Bind T_BindXtwin $baseout $moreout using `out3', excel append
			}
		}
	}

	if `"`bordafter'"' == "yes" {
		local cond agefirstbirth>=14&age<22
		cap rm "$Results/Outreg/`twinbordafter'.xls"
		cap rm "$Results/Outreg/`twinbordafter'.txt"

		local out "$Results/Outreg/`twinbordafter'.xls"
		foreach birth of numlist 2(1)8 {
			dis in yellow "We are on birth number `birth' of 8"	
			local cond2 (T_After==1&fert==`birth'+1)|(T_After==0&fert==`birth')

			foreach outcome of varlist school_zscore highschool childmort infantmort {
				reg `outcome' T_After $basecont $morecont [pw=sweight] if `cond'&`cond2'
				outreg2 T_After $baseout $moreout using `out', excel append

				reg `outcome' T_After $basecont [pw=sweight] if `cond'&`cond2'&e(sample)
				outreg2 T_After $baseout using `out', excel append

			}
		}
	}

	if `"`bordall'"' == "yes" {
		local cond agefirstbirth>=14&age<22
		cap rm "$Results/Outreg/`twinbordall'.xls"
		cap rm "$Results/Outreg/`twinbordall'.txt"

		local out "$Results/Outreg/`twinbordall'.xls"
		foreach birth of numlist 2(1)8 {
			dis in yellow "We are on birth number `birth' of 8"	
			local cond2 (T_Twin==1&fert==`birth'+1)|(T_Twin==0&fert==`birth')

			foreach outcome of varlist school_zscore highschool childmort infantmort {
				reg `outcome' T_Twin $basecont $morecont [pw=sweight] if `cond'&`cond2'
				outreg2 T_Twin $baseout $moreout using `out', excel append

				reg `outcome' T_Twin $basecont [pw=sweight] if `cond'&`cond2'&e(sample)
				outreg2 T_Twin $baseout using `out', excel append
			}
		}
	}	
}

********************************************************************************
**** (5a) Reduced Form Regressions on twin by birth order (by gender)
********************************************************************************
if `"`gend'"' == "yes" {	
	** remove outreg files
	foreach x in M F {
		preserve
		keep if gender=="`x'"
		local cond agefirstbirth>=14&age<22

		cap rm "$Results/Outreg/`gend_twinbordafter'_`x'.xls"
		cap rm "$Results/Outreg/`gend_twinbordafter'_`x'.txt"
		cap rm "$Results/Outreg/`gend_twinbordall'_`x'.xls"
		cap rm "$Results/Outreg/`gend_twinbordall'_`x'.txt"

		local loop 1
		foreach cond2 in cond_aft /*cond_all*/ {
			if `loop'==1 local out "$Results/Outreg/`gend_twinbordafter'_`x'.xls"
			else if `loop'==2 local out "$Results/Outreg/`gend_twinbordall'_`x'.xls"		

			foreach y of varlist school_zscore /*attendance highschool*/ {
				foreach birth of numlist 2(1)7 {

					local cond_aft (T_After==1&fert==`birth'+1)|(T_After==0&fert==`birth')
					local cond_all (T_Twin==1&fert==`birth'+1)|(T_Twin==0&fert==`birth')

					reg `y' T_After $basecont $morecont [pw=sweight] if `cond'&``cond2''
					outreg2 T_After using `out', excel append

					reg `y' T_After $basecont [pw=sweight] if `cond'&``cond2''&e(sample)
					outreg2 T_After using `out', excel append
				}
			}
			local ++loop
		}
		restore
	}
}

********************************************************************************
**** (5b) Reduced Form Regressions on twin by birth order (by income)
********************************************************************************
if `"`income'"' == "yes" {
	** remove outreg files
	foreach x in low mid {
		preserve
		keep if income=="`x'"
		local cond agefirstbirth>=14&age<22

		cap rm "$Results/Outreg/`inc_twinbordafter'_`x'.xls"
		cap rm "$Results/Outreg/`inc_twinbordafter'_`x'.txt"
		cap rm "$Results/Outreg/`inc_twinbordall'_`x'.xls"
		cap rm "$Results/Outreg/`inc_twinbordall'_`x'.txt"

		local loop 1
		foreach cond2 in cond_aft /*cond_all*/ {

			if `loop'==1 local out "$Results/Outreg/`inc_twinbordafter'_`x'.xls"
			else if `loop'==2 local out "$Results/Outreg/`inc_twinbordall'_`x'.xls"		

			foreach y of varlist school_zscore /*attendance highschool*/ {
				foreach birth of numlist 2(1)7 {

					local cond_aft (T_After==1&fert==`birth'+1)|(T_After==0&fert==`birth')
					local cond_all (T_Twin==1&fert==`birth'+1)|(T_Twin==0&fert==`birth')

					reg `y' T_After $basecont $morecont [pw=sweight] if `cond'&``cond2''
					outreg2 T_After using `out', excel append

					reg `y' T_After $basecont [pw=sweight] if `cond'&``cond2''&e(sample)
					outreg2 T_After using `out', excel append

				}
			}
			local ++loop
		}
		restore
	}
}


********************************************************************************
**** (6) Reduced Form Regressions on pre-twins by birth order
********************************************************************************
if `"`bordpretwin'"'=="yes" {
	keep if income=="low"
	if `"`bordafter_pre'"' == "yes" {		
		local cond agefirstbirth>=14&age<22
		cap rm "$Results/Outreg/`twinbordafter_pre'.xls"
		cap rm "$Results/Outreg/`twinbordafter_pre'.txt"

		local out "$Results/Outreg/`twinbordafter_pre'.xls"
		foreach birth of numlist 2(1)7 {
			dis in yellow "We are on birth number `birth' of 7"	
			local cond2 (T_After==1&fert==`birth'+1&pretwinafter==1)|(T_After==0&fert==`birth')

			foreach outcome of varlist school_zscore highschool childmort infantmort {
				reg `outcome' T_After $basecont $morecont [pw=sweight] if `cond'&`cond2'
				outreg2 T_After $baseout $moreout using `out', excel append


				reg `outcome' T_After $basecont [pw=sweight] if `cond'&`cond2'&e(sample)
				outreg2 T_After $baseout using `out', excel append

			}
		}
	}

	if `"`bordall_pre'"' == "yes" {
		local cond agefirstbirth>=14&age<22
		cap rm "$Results/Outreg/`twinbordall_pre'.xls"
		cap rm "$Results/Outreg/`twinbordall_pre'.txt"

		local out "$Results/Outreg/`twinbordall_pre'.xls"
		foreach birth of numlist 2(1)7 {
			dis in yellow "We are on birth number `birth' of 7"	
			local cond2 (T_Twin==1&fert==`birth'+1&pretwin==1)|(T_Twin==0&fert==`birth')

			foreach outcome of varlist school_zscore highschool childmort infantmort {
				reg `outcome' T_Twin $basecont $morecont [pw=sweight] if `cond'&`cond2'
				outreg2 T_Twin $baseout $moreout using `out', excel append


				reg `outcome' T_Twin $basecont [pw=sweight] if `cond'&`cond2'&e(sample)
				outreg2 T_Twin $baseout using `out', excel append
			}
		}
	}
}

********************************************************************************
**** (7) Instrument Q-Q
********************************************************************************
if `"`IV'"'=="yes" {
	if `"`IVbordafter'"' == "yes" {
		local c1 agefirstbirth>=14&age<22
		cap rm "$Results/Outreg/`IVtwinbordafter'.xls"
		cap rm "$Results/Outreg/`IVtwinbordafter'.txt"

		local out "$Results/Outreg/`IVtwinbordafter'.xls"
		foreach birth of numlist 2(1)7 {
			dis in yellow "We are on birth number `birth' of 8"	
			local c2 (T_After==1&fert==`birth'+1)|(T_After==0&fert==`birth')

			foreach y of varlist school_zscore attendance /*educ highschool*/ {
				xi: ivreg2 `y' $basecont $morecont (fert=twind) [pw=sweigh] if `c1'&`c2'
				outreg2 fert $baseout $moreout using `out', excel append

				xi: ivreg2 `y' $basecont (fert=twind) [pw=sweigh] if `c1'&`c2'&e(sample)
				outreg2 fert $baseout using `out', excel append
			}
		}
	}

	if `"`IVbordall'"' == "yes" {
		local c agefirstbirth>=14&age<22
		cap rm "$Results/Outreg/`IVtwinbordall'.xls"
		cap rm "$Results/Outreg/`IVtwinbordall'.txt"

		local out "$Results/Outreg/`IVtwinbordall'.xls"
		foreach birth of numlist 2(1)7 {
			dis in yellow "We are on birth number `birth' of 8"	
			local c2 (T_Twin==1&fert==`birth'+1)|(T_Twin==0&fert==`birth')

			foreach y of varlist school_zscore attendance /*educ highschool*/ {
				xi: ivreg2 `y' $basecont $morecont (fert=twind) [pw=sweight] if `c'&`c2'
				outreg2 fert $baseout $moreout using `out', excel append

				xi: ivreg2 `y' $basecont (fert=twind) [pw=sweight] if `c'&`c2'&e(sample)
				outreg2 fert $baseout using `out', excel append
			}
		}
	}	
}

********************************************************************************
**** (7) Instrument as per Angrist et al using "+ groups"
********************************************************************************
**** Here the idea is to form 
****
****
********************************************************************************
if `"`plusgroups'"'=="yes" {

}
